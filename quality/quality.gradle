apply plugin: 'checkstyle'
apply plugin: 'findbugs'
apply plugin: 'pmd'
check.dependsOn 'checkstyle', 'pmd'

def configDir = "${project.rootDir}/quality"
def reportsDir = "${project.buildDir}/reports"

checkstyle {
    toolVersion = '7.8'
}

pmd {
    toolVersion = '5.7.0'
}

task checkstyle(type: Checkstyle) {
    configFile file("$configDir/checkstyle.xml")
    source 'src'
    include '**/*.*'
    exclude '**/gen/**', '**/*.png'
    classpath = files()
}

task pmd(type: Pmd) {
    ignoreFailures = false
    ruleSetFiles = files("$configDir/pmd-ruleset.xml")
    ruleSets = []

    source 'src'
    include '**/*.java'
    exclude '**/gen/**'

    reports {
        xml.enabled = false
        html.enabled = true
        xml {
            destination file("$reportsDir/pmd.xml")
        }
        html {
            destination file("$reportsDir/pmd.html")
        }
    }
}

task findbugs(type: FindBugs, dependsOn: "assembleDebug") {
    ignoreFailures = false
    effort = "max"
    reportLevel = "high"
    classes = files("${project.rootDir}/app/build/intermediates/classes")

    source 'src'
    include '**/*.java'
    exclude '**/gen/**'
    excludeFilter = file("$configDir/findbugs-filter.xml")

    reports {
        xml.enabled = false
        html.enabled = true
        xml {
            destination file("$reportsDir/findbugs.xml")
        }
        html {
            destination file("$reportsDir/findbugs.html")
        }
    }

    classpath = files()
}

android {
    lintOptions {
        disable 'InvalidPackage', // Needed because of Okio
                'GradleDependency', 'NewerVersionAvailable' // For build reproducibility
        baseline file("$configDir/lint-baseline.xml")
        checkAllWarnings true
        warningsAsErrors true
        abortOnError true
        htmlOutput file("$reportsDir/lint-results.html")
        xmlOutput file("$reportsDir/lint-results.xml")
    }
}

apply plugin: 'checkstyle'
apply plugin: 'findbugs'
apply plugin: 'pmd'
check.dependsOn 'checkstyle', 'findbugs', 'pmd'

def configDir = "${project.rootDir}/quality"
def reportsDir = "${project.buildDir}/reports"

checkstyle {
    toolVersion = '7.6.1'
}

pmd {
    toolVersion = '5.5.5'
}

task checkstyle(type: Checkstyle) {
    configFile file("$configDir/checkstyle.xml")
    source 'src'
    include '**/*.*'
    exclude '**/gen/**', '**/*.png'
    classpath = files()
}

task findbugs(type: FindBugs, dependsOn: "assembleDebug") {
    ignoreFailures = false
    effort = "max"
    reportLevel = "high"
    classes = files("${project.rootDir}/app/build/intermediates/classes")

    source 'src'
    include '**/*.java'
    exclude '**/gen/**'
    excludeFilter = file("$configDir/findbugs-filter.xml")

    reports {
        xml.enabled = false
        html.enabled = true
        xml {
            destination "$reportsDir/findbugs.xml"
        }
        html {
            destination "$reportsDir/findbugs.html"
        }
    }

    classpath = files()
}

task pmd(type: Pmd) {
    ignoreFailures = false
    ruleSetFiles = files("$configDir/pmd-ruleset.xml")
    ruleSets = []

    source 'src'
    include '**/*.java'
    exclude '**/gen/**'

    reports {
        xml.enabled = false
        html.enabled = true
        xml {
            destination "$reportsDir/pmd.xml"
        }
        html {
            destination "$reportsDir/pmd.html"
        }
    }
}

android {
    lintOptions {
        disable 'InvalidPackage', // Needed because of Okio
                'GradleDependency', 'NewerVersionAvailable'
        baseline file("$configDir/lint-baseline.xml")
        checkAllWarnings true
        warningsAsErrors true
        abortOnError true
        htmlOutput file("$reportsDir/lint-results.html")
        xmlOutput file("$reportsDir/lint-results.xml")
    }
}

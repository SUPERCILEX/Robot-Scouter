def isReleaseBuild = !project.hasProperty('devBuild')

apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
if (isReleaseBuild) {
    apply plugin: 'com.google.firebase.firebase-crash'
    apply plugin: 'com.google.firebase.firebase-perf'
}
apply plugin: 'com.github.triplet.play'
tasks.whenTaskAdded { task ->
    if (task.name == "assembleRelease") task.dependsOn 'updateReleasePlayVersion'
}

apply from: '../quality/quality.gradle'

android {
    compileSdkVersion 26

    defaultConfig {
        manifestPlaceholders = [appName: '@string/app_name']
        applicationId 'com.supercilex.robotscouter'
        minSdkVersion 16
        targetSdkVersion 26
        versionCode 1 // The version code is updated on each Travis build
        versionName '2.0.0-beta1'
        multiDexEnabled true
        resConfigs "en"
        vectorDrawables.useSupportLibrary = true
    }

    signingConfigs {
        release {
            def keystorePropertiesFile = file('keystore.properties')
            def keystoreProperties = new Properties()
            keystoreProperties.load(new FileInputStream(keystorePropertiesFile))

            keyAlias keystoreProperties['keyAlias']
            keyPassword keystoreProperties['keyPassword']
            storeFile file(keystoreProperties['storeFile'])
            storePassword keystoreProperties['storePassword']
        }
    }

    buildTypes {
        debug {
            applicationIdSuffix '.debug'
            versionNameSuffix '-DEBUG'
            manifestPlaceholders = [appName: 'Robot Scouter DEBUG']
        }

        release {
            signingConfig signingConfigs.release
            postprocessing {
                removeUnusedCode true
                removeUnusedResources true
                obfuscate true
                optimizeCode true
                proguardFile 'proguard-rules.pro'
            }
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}

play {
    track = 'alpha'
    serviceAccountEmail = 'google-play-auto-publisher@robot-scouter-app.iam.gserviceaccount.com'
    jsonFile = file('google-play-auto-publisher.json')
}

dependencies {
    implementation fileTree(dir: 'libs')

    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlinVersion"

    // Support libraries
    implementation 'com.android.support:multidex:1.0.2'
    implementation "com.android.support:design:$supportLibVersion"
    implementation "com.android.support:customtabs:$supportLibVersion"
    implementation "com.android.support:cardview-v7:$supportLibVersion"
    implementation "com.android.support:palette-v7:$supportLibVersion"
    implementation "com.android.support:preference-v14:$supportLibVersion"
    implementation "com.android.support:support-emoji-appcompat:$supportLibVersion"
    implementation 'com.android.support.constraint:constraint-layout:1.1.0-beta3'

    implementation "android.arch.lifecycle:extensions:$archVersion"
    implementation "android.arch.lifecycle:common-java8:$archVersion"

    implementation 'com.android.billingclient:billing:dp-1'

    // Firebase UI
    // Auth
//    implementation 'com.firebaseui:firebase-ui-auth:3.0.0'
    implementation "com.github.SUPERCILEX.FirebaseUI-Android:firebase-ui-auth:$firebaseUiVersion"
    implementation "com.google.firebase:firebase-auth:$playServicesVersion"
    implementation "com.google.android.gms:play-services-auth:$playServicesVersion"
    implementation 'com.facebook.android:facebook-login:4.27.0'
    implementation("com.twitter.sdk.android:twitter-core:3.1.0@aar") { transitive = true }
    // Database
//    implementation 'com.firebaseui:firebase-ui-firestore:3.0.0'
    implementation "com.github.SUPERCILEX.FirebaseUI-Android:firebase-ui-firestore:$firebaseUiVersion"
    implementation "com.google.firebase:firebase-firestore:$playServicesVersion"

    implementation 'com.firebase:firebase-jobdispatcher:0.8.3'

    // Firebase
    implementation "com.google.firebase:firebase-core:$playServicesVersion"
    implementation "com.google.firebase:firebase-crash:$playServicesVersion"
    implementation "com.google.firebase:firebase-perf:$playServicesVersion"
    implementation "com.google.firebase:firebase-storage:$playServicesVersion"
    implementation "com.google.firebase:firebase-invites:$playServicesVersion"
    implementation "com.google.firebase:firebase-appindexing:$playServicesVersion"
    implementation "com.google.firebase:firebase-config:$playServicesVersion"
    implementation "com.google.firebase:firebase-messaging:$playServicesVersion"

    // From Google-ish
    implementation 'pub.devrel:easypermissions:1.0.1'
    implementation "com.github.bumptech.glide:glide:$glideVersion"
    implementation "com.github.bumptech.glide:recyclerview-integration:$glideVersion"

    // Third party
    implementation 'com.github.SUPERCILEX:kotterknife:5331f07fdf'
    implementation 'net.yslibrary.licenseadapter:licenseadapter:1.4.0'
    implementation 'com.github.clans:fab:1.6.4'
    implementation 'uk.co.samuelwall:material-tap-target-prompt:2.1.0'
    implementation 'com.github.rubensousa:gravitysnaphelper:1.3'

    // Apache POI - to update, use https://github.com/SUPERCILEX/poi-on-android
    compileOnly 'org.apache.poi:poi-ooxml:3.16-beta2' // Used for documentation

    // Leak canary
    debugImplementation "com.squareup.leakcanary:leakcanary-android:$leakCanaryVersion"
    releaseImplementation "com.squareup.leakcanary:leakcanary-android-no-op:$leakCanaryVersion"

    // Retrofit
    implementation "com.squareup.retrofit2:retrofit:$retrofitVersion"
    implementation "com.squareup.retrofit2:converter-gson:$retrofitVersion"
}

apply plugin: 'com.google.gms.google-services'

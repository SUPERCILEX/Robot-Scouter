apply plugin: 'com.android.application'
apply plugin: 'com.google.firebase.firebase-crash'
apply plugin: 'com.github.triplet.play'
apply plugin: 'project-report'
apply from: '../quality/quality.gradle'
build.dependsOn 'updateProdReleasePlayVersion'

def keystorePropertiesFile = file('keystore.properties')
def keystoreProperties = new Properties()
keystoreProperties.load(new FileInputStream(keystorePropertiesFile))

android {
    compileSdkVersion 25
    buildToolsVersion '25.0.2'

    defaultConfig {
        manifestPlaceholders = [appName: '@string/app_name']
        applicationId 'com.supercilex.robotscouter'
        minSdkVersion 16
        targetSdkVersion 25
        versionCode 1 // The version code is updated on each Travis build
        versionName '1.0.5'
        multiDexEnabled true
    }

    signingConfigs {
        release {
            keyAlias keystoreProperties['keyAlias']
            keyPassword keystoreProperties['keyPassword']
            storeFile file(keystoreProperties['storeFile'])
            storePassword keystoreProperties['storePassword']
        }
    }

    productFlavors {
        dev
        prod
    }

    buildTypes {
        debug {
            applicationIdSuffix '.debug'
            versionNameSuffix '-DEBUG'
            manifestPlaceholders = [appName: 'Robot Scouter DEBUG']
        }

        release {
            shrinkResources true
            minifyEnabled true
            proguardFiles 'proguard-rules.pro', getDefaultProguardFile('proguard-android-optimize.txt')
            signingConfig signingConfigs.release
        }
    }

    dexOptions {
        javaMaxHeapSize '3g'
        jumboMode true
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}

play {
    track = 'alpha'
    serviceAccountEmail = 'google-play-auto-publisher@robot-scouter-app.iam.gserviceaccount.com'
    jsonFile = file('google-play-auto-publisher.json')
}

configurations {
    devDebugCompile
    prodDebugCompile
    devReleaseCompile
    prodReleaseCompile
}

android.variantFilter { variant ->
    if (variant.name == 'devRelease' || variant.name == 'prodDebug') variant.setIgnore(true)
}

android.applicationVariants.all { variant ->
    variant.preBuild.doLast {
        variant.mergedFlavor.versionName = "${android.defaultConfig.versionName}.$variant.versionCode"
    }
}

project.afterEvaluate {
//    publishApkProdRelease.dependsOn 'firebaseUploadProdReleaseProguardMapping' TODO add back when Android fixes their plugin
}

dependencies {
    // To update Apache POI, use https://github.com/SUPERCILEX/poi-on-android
    compile fileTree(dir: 'libs', include: ['*.jar'])

    // Support libraries
    compile 'com.android.support:multidex:1.0.1'
    compile "com.android.support:design:$supportLibVersion"
    compile "com.android.support:customtabs:$supportLibVersion"
    compile "com.android.support:palette-v7:$supportLibVersion"
    compile 'com.android.support.constraint:constraint-layout:1.0.2'

    // Firebase UI
    // Auth
//    compile 'com.firebaseui:firebase-ui-auth:2.0.0'
    compile 'com.github.SUPERCILEX.FirebaseUI-Android:firebase-ui-auth:549cb4525bc63bcbcdf06d689d1671f514b1411f'
    compile "com.google.firebase:firebase-auth:$playServicesVersion"
    compile "com.google.android.gms:play-services-auth:$playServicesVersion"
    compile "com.android.support:cardview-v7:$supportLibVersion"
    // Database
//    compile 'com.firebaseui:firebase-ui-database:2.0.0'
    compile 'com.github.SUPERCILEX.FirebaseUI-Android:firebase-ui-database:549cb4525bc63bcbcdf06d689d1671f514b1411f'
    compile "com.google.firebase:firebase-database:$playServicesVersion"

    compile 'com.firebase:firebase-jobdispatcher:0.5.2'

    // Firebase core
    compile "com.google.firebase:firebase-core:$playServicesVersion"
    compile "com.google.firebase:firebase-crash:$playServicesVersion"
    compile "com.google.firebase:firebase-invites:$playServicesVersion"
    compile "com.google.firebase:firebase-appindexing:$playServicesVersion"
    compile "com.google.firebase:firebase-config:$playServicesVersion"
    compile "com.google.firebase:firebase-messaging:$playServicesVersion"

    // From Google-ish
    compile 'pub.devrel:easypermissions:0.3.0'
    compile 'com.github.bumptech.glide:glide:3.7.0'

    // Third party
    compile 'de.hdodenhof:circleimageview:2.1.0'
    compile 'net.yslibrary.licenseadapter:licenseadapter:1.4.0'
    compile 'com.github.clans:fab:1.6.4'
    compile 'uk.co.samuelwall:material-tap-target-prompt:1.9.5'

    // Leak canary
    debugCompile "com.squareup.leakcanary:leakcanary-android:$leakCanaryVersion"
    prodReleaseCompile "com.squareup.leakcanary:leakcanary-android-no-op:$leakCanaryVersion"

    // Retrofit
    compile "com.squareup.retrofit2:retrofit:$retrofitVersion"
    compile "com.squareup.retrofit2:converter-gson:$retrofitVersion"
}

apply plugin: 'com.google.gms.google-services'

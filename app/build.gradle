apply plugin: 'com.android.application'

def keystorePropertiesFile = rootProject.file("keystore.properties")
def keystoreProperties = new Properties()
keystoreProperties.load(new FileInputStream(keystorePropertiesFile))

android {
    compileSdkVersion 25
    buildToolsVersion "25.0.0"

    defaultConfig {
        manifestPlaceholders = [appName: "@string/app_name"]
        applicationId "com.supercilex.robotscouter"
        targetSdkVersion 25
        minSdkVersion 16
        versionCode buildVersionCode()
        versionName "1.0.0"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }

    signingConfigs {
        config {
            keyAlias keystoreProperties['keyAlias']
            keyPassword keystoreProperties['keyPassword']
            storeFile file(keystoreProperties['storeFile'])
            storePassword keystoreProperties['storePassword']
        }
    }

    buildTypes {
        release {
            shrinkResources true
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.config
        }

        debug {
            applicationIdSuffix '.debug'
            versionNameSuffix '-DEBUG'
            manifestPlaceholders = [appName: "Robot Scouter DEBUG"]
        }
    }

    lintOptions {
        htmlReport true
        htmlOutput file("lint-report.html")
        abortOnError true
        warningsAsErrors true
    }
}

dependencies {
    // Support libraries
    compile "com.android.support:design:${project.supportLibVersion}"
    compile "com.android.support:customtabs:${project.supportLibVersion}"
    compile 'com.android.support.constraint:constraint-layout:1.0.0-beta1'

    // Firebase
    compile "com.google.firebase:firebase-core:${project.playServicesVersion}"
    compile "com.google.firebase:firebase-messaging:${project.playServicesVersion}"
    compile "com.google.firebase:firebase-config:${project.playServicesVersion}"
    compile "com.google.firebase:firebase-database:${project.playServicesVersion}"
    compile "com.google.firebase:firebase-auth:${project.playServicesVersion}"
    compile "com.google.android.gms:play-services-auth:${project.playServicesVersion}"
    compile "com.google.firebase:firebase-crash:${project.playServicesVersion}"
    compile "com.google.firebase:firebase-invites:${project.playServicesVersion}"
    compile 'com.firebaseui:firebase-ui:0.6.1'

    // Third party
    compile 'de.hdodenhof:circleimageview:2.1.0'
    compile 'com.github.bumptech.glide:glide:3.7.0'
    compile 'com.mikepenz:materialdrawer:5.6.0'
//    debugCompile 'com.squareup.leakcanary:leakcanary-android:1.4'

    // Retrofit
    compile "com.squareup.retrofit2:retrofit:${retrofitVersion}"
    compile "com.squareup.retrofit2:converter-gson:${retrofitVersion}"

    // Testing
    testCompile 'junit:junit:4.12'
    androidTestCompile 'com.android.support.test.espresso:espresso-core:2.2.2'
}

apply plugin: 'com.google.gms.google-services'

def isReleaseBuild = !project.hasProperty('devBuild')

apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
if (isReleaseBuild) {
    apply plugin: 'com.google.firebase.firebase-crash'
    apply plugin: 'com.google.firebase.firebase-perf'
}
apply plugin: 'com.github.triplet.play'
build.dependsOn 'updateReleasePlayVersion'

apply from: '../quality/quality.gradle'

android {
    compileSdkVersion 26
    buildToolsVersion '26.0.0' // Note: remember to update travis.yml and upload.sh versions too

    defaultConfig {
        manifestPlaceholders = [appName: '@string/app_name']
        applicationId 'com.supercilex.robotscouter'
        minSdkVersion 16
        targetSdkVersion 25
        versionCode 1 // The version code is updated on each Travis build
        versionName '1.1.2'
        multiDexEnabled true
        vectorDrawables.useSupportLibrary = true
    }

    signingConfigs {
        release {
            def keystorePropertiesFile = file('keystore.properties')
            def keystoreProperties = new Properties()
            keystoreProperties.load(new FileInputStream(keystorePropertiesFile))

            keyAlias keystoreProperties['keyAlias']
            keyPassword keystoreProperties['keyPassword']
            storeFile file(keystoreProperties['storeFile'])
            storePassword keystoreProperties['storePassword']
        }
    }

    buildTypes {
        debug {
            applicationIdSuffix '.debug'
            versionNameSuffix '-DEBUG'
            manifestPlaceholders = [appName: 'Robot Scouter DEBUG']
        }

        release {
            shrinkResources true
            minifyEnabled true
            proguardFiles 'proguard-rules.pro', getDefaultProguardFile('proguard-android-optimize.txt')
            signingConfig signingConfigs.release
        }
    }

    dexOptions {
        jumboMode true
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}

play {
    track = 'alpha'
    serviceAccountEmail = 'google-play-auto-publisher@robot-scouter-app.iam.gserviceaccount.com'
    jsonFile = file('google-play-auto-publisher.json')
}

if (isReleaseBuild) {
    android.applicationVariants.all { variant ->
        variant.preBuild.doLast {
            variant.mergedFlavor.versionName = "${android.defaultConfig.versionName}.$variant.versionCode"
        }
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])

    implementation "org.jetbrains.kotlin:kotlin-stdlib-jre8:$kotlinVersion"

    // Support libraries
    implementation 'com.android.support:multidex:1.0.1'
    implementation "com.android.support:design:$supportLibVersion"
    implementation "com.android.support:customtabs:$supportLibVersion"
    implementation "com.android.support:cardview-v7:$supportLibVersion"
    implementation "com.android.support:palette-v7:$supportLibVersion"
    implementation 'com.android.support.constraint:constraint-layout:1.1.0-beta1'

    // Firebase UI
    // Auth
//    implementation 'com.firebaseui:firebase-ui-auth:2.0.0'
    implementation "com.github.SUPERCILEX.FirebaseUI-Android:firebase-ui-auth:$firebaseUiVersion"
    implementation "com.google.firebase:firebase-auth:$playServicesVersion"
    implementation "com.google.android.gms:play-services-auth:$playServicesVersion"
    implementation 'com.facebook.android:facebook-android-sdk:4.24.0'
    implementation("com.twitter.sdk.android:twitter-core:3.0.0@aar") { transitive = true }
    // Database
//    implementation 'com.firebaseui:firebase-ui-database:2.0.0'
    implementation "com.github.SUPERCILEX.FirebaseUI-Android:firebase-ui-database:$firebaseUiVersion"
    implementation "com.google.firebase:firebase-database:$playServicesVersion"

    implementation 'com.firebase:firebase-jobdispatcher:0.6.0'

    // Firebase
    implementation "com.google.firebase:firebase-core:$playServicesVersion"
    implementation "com.google.firebase:firebase-crash:$playServicesVersion"
    implementation "com.google.firebase:firebase-perf:$playServicesVersion"
    implementation "com.google.firebase:firebase-invites:$playServicesVersion"
    implementation "com.google.firebase:firebase-appindexing:$playServicesVersion"
    implementation "com.google.firebase:firebase-config:$playServicesVersion"
    implementation "com.google.firebase:firebase-messaging:$playServicesVersion"

    // From Google-ish
    implementation 'pub.devrel:easypermissions:0.4.1'
    implementation 'com.github.bumptech.glide:glide:4.0.0-RC1'

    // Third party
    implementation 'net.yslibrary.licenseadapter:licenseadapter:1.4.0'
    implementation 'com.github.clans:fab:1.6.4'
//    implementation 'uk.co.samuelwall:material-tap-target-prompt:1.10.0'
    implementation 'com.github.SUPERCILEX:MaterialTapTargetPrompt:5fbbfdd7ba'

    // Apache POI - to update, use https://github.com/SUPERCILEX/poi-on-android
    compileOnly 'org.apache.poi:poi-ooxml:3.16-beta2' // Used for documentation

    // Leak canary
    debugImplementation "com.squareup.leakcanary:leakcanary-android:$leakCanaryVersion"
    releaseImplementation "com.squareup.leakcanary:leakcanary-android-no-op:$leakCanaryVersion"

    // Retrofit
    implementation "com.squareup.retrofit2:retrofit:$retrofitVersion"
    implementation "com.squareup.retrofit2:converter-gson:$retrofitVersion"
}

apply plugin: 'com.google.gms.google-services'

buildscript {
    repositories {
        jcenter()
    }

    dependencies {
        classpath 'com.android.tools.build:gradle:2.2.2'
        classpath 'com.google.gms:google-services:3.0.0'
    }
}

allprojects {
    repositories {
        jcenter()
        flatDir {
            dirs 'libs'
        }
        maven { url 'https://maven.fabric.io/public' }
    }
}

task clean(type: Delete) {
    delete rootProject.buildDir
}

ext {
    supportLibVersion = "25.0.0"
    playServicesVersion = "9.8.0"
    retrofitVersion = "2.1.0"

    // Build the version code
    buildVersionCode = {
        def versionPropsFile = file('gradle.properties')

        if (versionPropsFile.canRead()) {
            Properties versionProps = new Properties()

            versionProps.load(new FileInputStream(versionPropsFile))

            def code = versionProps['VERSION_CODE'].toInteger() + 1

            versionProps['VERSION_CODE'] = code.toString()
            versionProps.store(versionPropsFile.newWriter(),
                    "# suppress inspection \"UnusedProperty\" for whole file")

            return code
        } else {
            throw new GradleException("Could not read gradle.properties!")
        }
    }
}